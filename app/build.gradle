apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("../iPay_KeyStore/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def appPropertiesFile = rootProject.file("app-settings.properties")
def appProperties = new Properties()
appProperties.load(new FileInputStream(appPropertiesFile))
def envProperties

android {
    compileSdkVersion appProperties['sdk.version.compile'] as int
    buildToolsVersion appProperties['build.tool.version']

    signingConfigs {
        iPaySigningConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    defaultConfig {
        applicationId "bd.com.ipay.android"
        minSdkVersion appProperties['sdk.version.min'] as int
        targetSdkVersion appProperties['sdk.version.target'] as int
        multiDexEnabled true
        versionCode appProperties['app.version.code'] as int
        versionName appProperties['app.version.name']
        vectorDrawables.useSupportLibrary = true

        def buildVariant = getBuildVariant()
        def environmentPath
        environmentPath = appProperties["env.path.$buildVariant"]

        def envPropertiesFile = rootProject.file(environmentPath)
        envProperties = new Properties()
        envProperties.load(new FileInputStream(envPropertiesFile))
        println("buildVariant = $buildVariant")
        for (String key : envProperties.keySet()) {
            buildConfigField "String", key.replaceAll("\\.", "_").toUpperCase(), "\"" + envProperties[key] + "\""
        }
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.iPaySigningConfig
        }
        BuildTest {
            debuggable true
            signingConfig signingConfigs.iPaySigningConfig
        }
        BuildDev {
            debuggable true
            signingConfig signingConfigs.iPaySigningConfig
        }
        BuildStage {
            debuggable true
            signingConfig signingConfigs.iPaySigningConfig
        }
        BuildDemo {
            debuggable true
            signingConfig signingConfigs.iPaySigningConfig
        }
        BuildLocal {
            debuggable true
            signingConfig signingConfigs.iPaySigningConfig
        }
        release {
            signingConfig signingConfigs.iPaySigningConfig
            minifyEnabled appProperties["minify.enabled"] as boolean
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }
    sourceSets { main { assets.srcDirs = ['src/main/assets', 'src/main/assets/fonts'] } }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.aspectj:aspectjtools:1.8.1'
    }
}

android.packagingOptions {
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/license.txt'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/notice.txt'
    exclude 'META-INF/ASL2.0'
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

configurations.all {
    resolutionStrategy {
        force 'com.android.support:design:27.1.1'
        force 'com.android.support:support-v4:27.1.1'
        force 'com.android.support:appcompat-v7:27.1.1'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':camera')
    compile 'com.android.support:design:27.1.1'
    compile 'com.android.support:cardview-v7:27.1.1'
    compile 'com.android.support:recyclerview-v7:27.1.1'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.journeyapps:zxing-android-embedded:3.0.1@aar'
    compile 'com.google.zxing:core:3.2.0'
    compile 'com.airbnb.android:lottie:2.5.0-rc2'
    compile 'com.makeramen:roundedimageview:2.2.1'
    compile 'com.github.bumptech.glide:glide:3.8.0'
    compile 'de.hdodenhof:circleimageview:2.2.0'
    compile('com.github.afollestad.material-dialogs:core:0.8.5.3@aar') {
        transitive = true
    }
    compile('org.apache.httpcomponents:httpmime:4.3.6') {
        exclude module: 'httpclient'
    }
    // Bottom Sheet
    compile 'com.flipboard:bottomsheet-core:1.5.0'
    compile 'com.flipboard:bottomsheet-commons:1.5.0'

    compile files('libs/libphonenumber-7.2.8.jar')
    compile 'com.github.johnkil.android-progressfragment:progressfragment:1.4.0'
    compile 'com.google.firebase:firebase-messaging:11.8.0'
    compile 'com.google.android.gms:play-services-places:11.8.0'
    compile 'com.google.android.gms:play-services-vision:11.8.0'
    compile 'com.google.android.gms:play-services-analytics:11.8.0'
    compile 'com.google.android.gms:play-services:11.8.0'
    compile 'com.squareup.okhttp3:okhttp:3.10.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.firebase:firebase-client-android:2.4.0'
    compile 'com.github.drawers:SpinnerDatePicker:1.0.6'
    // Notification badge
    compile 'com.mikepenz:actionitembadge:3.2.6@aar'
    compile 'com.mikepenz:iconics-core:2.6.6@aar'

    // Aspect
    compile 'org.aspectj:aspectjrt:1.8.1'

    // Multiple image picker
    compile 'com.github.esafirm.android-image-picker:imagepicker:1.3.2@aar'

    // SSL pinning
    compile 'com.squareup.okhttp:okhttp:2.1.0'

    //Intercom
    compile 'io.intercom.android:intercom-sdk-base:5.+'
    compile 'io.intercom.android:intercom-sdk-fcm:5.+'

    // Singapore
    compile 'com.hbb20:ccp:1.7.8'
    //facebook sdk
    compile 'com.facebook.android:facebook-android-sdk:[4,5)'
}

def getBuildVariant() {
    for (TaskExecutionRequest t : gradle.getStartParameter().getTaskRequests()) {
        for (String command : t.args) {
            if (command.matches(":app:generate(.*)Sources")) {
                return command.replaceAll(":app:generate(.*)Sources", "\$1")
                        .toLowerCase()
                        .replaceAll("debug", "test").replaceAll("build", "")
            } else if (command.matches(":app:assemble(.*)")) {
                return command.replaceAll(":app:assemble(.*)", "\$1")
                        .toLowerCase()
                        .replaceAll("debug", "test").replaceAll("build", "")
            }
        }
    }

    return "release"
}

apply plugin: 'com.google.gms.google-services'
